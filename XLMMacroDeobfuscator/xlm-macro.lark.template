start:  "=" expression
function_call:  NAME L_PRA arglist R_PRA
method_call:    NAME DOT NAME L_PRA arglist R_PRA
arglist:    (argument LIST_SEPARATOR)* argument
argument:   expression |
cell:   a1_notation_cell | r1c1_notation_cell
a1_notation_cell:   [NAME "!" | "'" /[^']+/i "'!"| "!"] /\$?([a-qs-z][a-z]?)\$?\d+|\$?(r[a-bd-z]?)\$?\d+(?!C)/i | NAME    // NAME here is a hack to make it work with LALR parser, A9 is a valid NAME and matches with the CELL regex
r1c1_notation_cell: [NAME "!" | "'" /[^']+/i "'!" | "!"] ROW [REF | INT ] COL [REF | INT ]
?expression:   concat_expression CMPOP concat_expression | concat_expression
?concat_expression: additive_expression (CONCATOP additive_expression)*
?additive_expression:   multiplicative_expression (ADDITIVEOP multiplicative_expression)*
?multiplicative_expression: final (MULTIOP final)*
?final: L_PRA expression R_PRA | function_call | method_call | cell | range | atom
?range: cell COLON cell | cell COLON cell COLON cell
?atom: NUMBER | STRING | BOOLEAN
ADDITIVEOP: "+" | "-"
MULTIOP:    "*" | "/"
CMPOP:      "<" [">"] | ">" | "="
CONCATOP:   "&"
COLON:      ":"
STRING:   /\"([^\"]|\"\")*\"/i
BOOLEAN: "TRUE" | "FALSE"
ROW: "R" | "r"
COL: "C" | "c"
L_PRA: "("
R_PRA: ")"
L_BRAC: "{{XLLEFTBRACKET}}"
R_BRAC: "{{XLRIGHTBRACKET}}"
DOT: "."
LIST_SEPARATOR: "{{XLLISTSEPARATOR}}"
REF: L_BRAC SIGNED_INT  R_BRAC
%import common.SIGNED_INT -> SIGNED_INT
%import common.INT -> INT
%import common.DECIMAL -> DECIMAL
SIGNED_DECIMAL: ["+"|"-"] DECIMAL
NUMBER: SIGNED_DECIMAL | SIGNED_INT
NAME: /[_a-qs-z][_a-z0-9]*|r(?!(\[|\d*c\d*))[_a-z0-9]*/i
%ignore " "   // Disregard spaces in text