start:  "=" expression
function_call:  NAME L_PRA arglist R_PRA
method_call:    NAME DOT NAME L_PRA arglist R_PRA
arglist:    (argument LIST_SEPARATOR)* argument
argument:   expression |
cell:   a1_notation_cell | r1c1_notation_cell
a1_notation_cell:   [NAME "!" | "'" NAME "'!"] /\$?([a-qs-z][a-z]?)\$?\d+|\$?(r[a-bd-z]?)\$?\d+(?!C\d)/i | NAME    // NAME here is a hack to make it work with LALR parser, A9 is a valid NAME and matches with the CELL regex
r1c1_notation_cell: [NAME "!" | "'" NAME "'!"] ROW [REF | INT ] COL [REF | INT ]
?expression:   concat_expression CMPOP concat_expression | concat_expression
?concat_expression: additive_expression (CONCATOP additive_expression)*
?additive_expression:   multiplicative_expression (ADDITIVEOP multiplicative_expression)*
?multiplicative_expression: final (MULTIOP final)*
?final: L_PRA expression R_PRA | function_call | method_call | cell | range | atom
?range: cell COLON cell
?atom: NUMBER | STRING | BOOLEAN
ADDITIVEOP: "+" | "-"
MULTIOP:    "*" | "/"
CMPOP:      "<" [">"] | ">" | "="
CONCATOP:   "&"
COLON:      ":"
STRING:   /\"([^\"]|\"\")*\"/i
BOOLEAN: "TRUE" | "FALSE"
ROW: "R"
COL: "C"
L_PRA: "("
R_PRA: ")"
L_BRAC: "["
R_BRAC: "]"
DOT: "."
LIST_SEPARATOR: ","
REF: L_BRAC SIGNED_INT  R_BRAC
%import common.CNAME -> NAME
%import common.SIGNED_INT -> SIGNED_INT
%import common.INT -> INT
%import common.DECIMAL -> DECIMAL
SIGNED_DECIMAL: ["+"|"-"] DECIMAL
NUMBER: SIGNED_DECIMAL | SIGNED_INT
%ignore " "   // Disregard spaces in text